%%***************************************************************************************
%% file         sfcn_modbus_server_read_map.tlc
%% brief        Target Language Compiler file that contains the code generation specifics
%%              for an S-function with the same name.
%%
%%---------------------------------------------------------------------------------------
%%                          C O P Y R I G H T
%%---------------------------------------------------------------------------------------
%%  Copyright 2024 (c) by GOcontroll      http://www.gocontroll.com     All rights reserved
%%
%%---------------------------------------------------------------------------------------
%%                            L I C E N S E
%%---------------------------------------------------------------------------------------
%% Permission is hereby granted, free of charge, to any person obtaining a copy of this
%% software and associated documentation files (the "Software"), to deal in the Software
%% without restriction, including without limitation the rights to use, copy, modify, merge,
%% publish, distribute, sublicense, and/or sell copies of the Software, and to permit
%% persons to whom the Software is furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in all copies or
%% substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
%% INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
%% PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
%% FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
%% OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
%% DEALINGS IN THE SOFTWARE.
%% endinternal
%%
%%***************************************************************************************

%implements sfcn_modbus_server_read_map "C"
%include "modbus_common.tlc"

%%This function checks if one of the read blocks is trying to access data outside of the allocated area that is configured in the server config block
%%It gets calculated in bytes, so sometimes the number of registers needs to be multiplied by two, same goes for the offset.
%%The number of bytes are calculated in the ReadMemoryBlock function.
%function CheckTabSize(block, bytes, offset) void
	%switch (%<block.ParamSettings.block_type>)
		%case 1 %%Coil
			%if ::go_modbus_coils_num < %<bytes> + %<offset> %%coils are bytes, so no doubling
				%assign err = "there are %<::go_modbus_coils_num> coil registers defined in your config, but this block is trying to access a register out of this range"
				%<LibBlockReportError(block, err)>
			%endif
			%break
		%case 2 %%Discrete input
			%if ::go_modbus_discrete_in_num < %<bytes> + %<offset> %%discrete inputs are bytes, so no doubling
				%assign err = "there are %<::go_modbus_discrete_in_num> discrete input registers defined in your config, but this block is trying to access a register out of this range"
				%<LibBlockReportError(block, err)>
			%endif
			%break
		%case 3 %%Holding register
			%if ::go_modbus_holding_num * 2 < %<bytes> + %<offset> * 2 %%holding registers are 16 bits so the number of registers and the offset need to be multiplied by 2
				%assign err = "there are %<::go_modbus_holding_num> holding registers defined in your config, but this block is trying to access a register out of this range"
				%<LibBlockReportError(block, err)>
			%endif
			%break
		%case 4 %%Input register
			%if ::go_modbus_input_num * 2 < %<bytes> + %<offset> * 2 %%input registers are 16 bits so the number of registers and the offset need to be multiplied by 2
				%assign err = "there are %<::go_modbus_input_num> input registers defined in your config, but this block is trying to access a register out of this range"
				%<LibBlockReportError(block, err)>
			%endif
			%break
		%default
			%<LibBlockReportError(block, "Invalid memory block type!")>
	%endswitch
%endfunction

%%This function actually expands to the memory read operation depending on the register type, data type, address offset, and length
%function ReadMemoryBlock(block, tab) Output
	%%assign these parameters so they can be passed to a function and to have more compact code
	%assign offset = block.ParamSettings.address
	%assign length = block.ParamSettings.length
	%assign bit = block.ParamSettings.bit
	%assign dest = LibBlockOutputSignalAddr(0, "", "", 0)
	%switch (%<block.ParamSettings.data_type>)
		%case 1 %%double
			%<CheckTabSize(block, length*8, offset)>
			memcpy(%<dest>, map->%<tab> + %<offset>, %<length>*sizeof(double));
			%break
		%case 2 %%single/float
			%<CheckTabSize(block, length*4, offset)>
			memcpy(%<dest>, map->%<tab> + %<offset>, %<length>*sizeof(float));
			%break
		%case 3 %%int8
		%case 4 %%uint8
			%<CheckTabSize(block, length, offset)>
			memcpy(%<dest>, map->%<tab> + %<offset>, %<length>*sizeof(char));
			%break
		%case 5 %%int16
		%case 6 %%uint16
			%<CheckTabSize(block, length*2, offset)>
			memcpy(%<dest>, map->%<tab> + %<offset>, %<length>*sizeof(short));
			%break
		%case 7 %%int32
		%case 8 %%uint32
			%<CheckTabSize(block, length*4, offset)>
			memcpy(%<dest>, map->%<tab> + %<offset>, %<length>*sizeof(int));
			%break
		%case 9 %%boolean
			%<CheckTabSize(block, 1, offset)>
			%%output = (table[offset] & (1 << bit)) > 0
			%<LibBlockOutputSignal(0,"","",0)> = (map->%<tab>[%<offset>] & (1 << %<bit>)) > 0;
			%break
		%default
			%<LibBlockReportError(block, "Invalid data type!")>
	%endswitch
%endfunction

%% Function: BlockTypeSetup ==========================================================
%%
%% Purpose:
%%      Code generation for configuration
%%
%function BlockTypeSetup(block, system) void
	%<ModbusServerCommonBlockTypeSetup()>
%endfunction

%function BlockInstanceSetup(block, system) void
%endfunction

%% Function: Start ==========================================================
%%
%% Purpose:
%%      initialize the system
%%
%function Start(block, system) Output
	%if EXISTS(::go_modbus_server_config_present) == 0
		%<LibBlockReportError(block, "Modbus server configuration block is missing!")>
	%endif
%endfunction

%% Function: Output ==========================================================
%%
%% Purpose:
%%      Code generation for signal output
%%
%function Outputs(block, system) Output
	%%modbus map is shared between threads, so lock it
	pthread_mutex_lock(&map_lock);
	%switch (%<block.ParamSettings.block_type>)
		%case 1 %%coil
			%<ReadMemoryBlock(block, "tab_bits")>
			%break
		%case 2 %%discrete
			%<ReadMemoryBlock(block, "tab_input_bits")>
			%break
		%case 3 %%holding
			%<ReadMemoryBlock(block, "tab_registers")>
			%break
		%case 4 %%input
			%<ReadMemoryBlock(block, "tab_input_registers")>
			%break
		%default
			%<LibBlockReportError(block, "Invalid memory block type!")>
	%endswitch
	pthread_mutex_unlock(&map_lock);
%endfunction

%% Function: Terminate ==========================================================
%%
%% Purpose:
%%      Code generation for system termination
%%
%function Terminate (block, system) Output
%endfunction